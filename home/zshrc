# (almost) strict mode

set -uo pipefail

# set opts (warning: dangerous)

setopt autocd
setopt extendedglob
setopt menucomplete
setopt rmstarsilent

# auto-completion

if [[ "$USER" != "root" ]]; then
    autoload -U compinit
    compinit
    zstyle ":completion:*" insert-tab pending
    zstyle ":completion:*" matcher-list "m:{a-zA-Z}={A-Za-z}"
    zstyle ":completion:*" menu select
fi

# environment variables

export \
    EDITOR="edit" \
    HOMEBREW_CASK_OPTS="--appdir=/Applications" \
    HOMEBREW_NO_ANALYTICS="1" \
    LESS="--tabs=4" \
    LESSHISTFILE="/dev/null" \
    LS_COLORS="di=34:ln=35:so=32:pi=33:ex=31:bd=30;46:cd=30;43:su=30;41:sg=30;46:tw=30;42:ow=30;43:st=30;44:" \
    MORE="--tabs=4" \
    RLWRAP_HOME="/dev/null"
PS1=$'%{\e[37m%}%2~%{\e[0m%} %{\e[%(?.32.31)m%}%(!.#.$)%{\e[0m%} '

# alias

alias \
    c="clear" \
    df="df -h" \
    du="du -sh" \
    egrep="egrep --color=auto" \
    fgrep="fgrep --color=auto" \
    grep="grep --color=auto" \
    ls="ls -bh --color=auto --group-directories-first"

# git

alias \
    git="hub" \
    ga="hub add" \
    gc="hub commit" \
    gd="hub diff" \
    gk="hub checkout" \
    gl="hub log --decorate --oneline" \
    gr="hub reset" \
    gs="hub status -sb"

# "user@host" window title

precmd() { print -Pn "\e]2;%n@%M\a" }

# destroy some files

fuck() {
    f=
    r=false
    v=
    while getopts frv opt; do
        case "$opt" in
        (f) f="-f" ;;
        (r) r=true ;;
        (v) v="-v" ;;
        (*) return 1 ;;
        esac
    done
    shift $((OPTIND-1))
    if [[ ${#@} == 0 ]]; then
        echo "$0: missing operand"
        return 1
    fi
    if $r; then
        find $@ -type f -exec shred $f $v -zun 8 {} \;
        rm $f $v -r $@
    else
        shred $f $v -zun 8 $@
    fi
}

# trash some files

trash() {
    if [[ ${#@} == 0 ]]; then
        echo "$0: missing operand"
        return 1
    fi
    mv $@ "$HOME/.Trash"
}

# z

f="$HOME/.config/z/zshrc"
[[ -f "$f" ]] && source "$f"

# auto-update

date --iso=s
[[ "$USER" == "root" ]] || "$HOME/.config/libexec/auto-update"

# intentional lack of ending "true"
